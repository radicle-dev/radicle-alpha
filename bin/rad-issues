#!/usr/bin/env radicle

(load! "rad/prelude.rad")
(load! "rad/monadic/issues.rad")
(load! "rad/prelude/io-utils.rad")

(import prelude/validation :as 'validation)

(def base-name "http://staging.machines.radicle.xyz/chains/")

(def name-base "http://staging.machines.radicle.xyz/chains/names")

(def help
  (string-append
  "rad-issues - Radicle issue CLI

   Usage:
        rad-issues list <chain-name> [--closed] [--open] [--new]
        rad-issues [new|create-chain] <chain-name>
        rad-issues close <issue-number> <chain-name>
        rad-issues make-key <nick>
        rad-issues names
        rad-issues done
        rad-issues create-chain <chain-name>
        rad-issues help

     list         - Select from issues to view or comment on
     done         - Mark all issues as 'seen'
     new          - Create a new issue in $EDITOR
     close        - Close an issue
     create-chain - Create a new issues chain
     make-key     - Create a key-pair and register the name
     names        - See all registered names
     help         - Print this help and exit

   If <chain-name> begins with 'http://', that URL will be used. Otherwise,
  " base-name " will be prepended to <chain-name>." ))

(def make-name
  (fn [chain]
    (if (eq? (take 7 chain) "http://")
        chain
        (string-append base-name chain))))

(def make-chain-ref
  (fn [chain]
    (ref (chain/load-chain! (make-name chain)))))

(def mark-last-seen
  (fn []
    (write-file-key! ".radicle-issues" :last-mark-done (now!))))

(def comment-divider "---")

(def add-comment-md
  (fn [x]
    (string-append
     "\n" comment-divider "\n"
     "---Add comment above the line.\n\n"
     x)))

(def pretty-comment
  "Markdown view of a comment. Assumes that the comment has been annotated with a
  username."
  (fn [c]
    (match c
      '{:username u-n
        :created-at c-a
        :body b}
      (string-append "### " u-n " [" c-a "]\n\n" b "\n"))))

(def pretty-issue
  "Markdown view of an issue. Assumes the issue and comments have been annotated
  with usernames."
  (fn [i]
    (match i
      '{:number n
        :title tit
        :body b
        :state state
        :labels ls
        :comments cs
        :username un
        :created-at c-a}
      (string-replace
       "\r"
       ""
       (string-append
        "# Issue " (show n) ": " tit "\n\n"
        "created by " un " [" c-a "]\n\n"
        "**State:** " (if (eq? state :open) "open" "closed") "\n"
        "**Labels:** " (show ls) "\n\n"
        b "\n\n"
        "Comments\n"
        "--------\n\n"
        (string/unlines (map pretty-comment cs)))))))

(def list-items
  "Lists the recent items and uses an editor to add a relevant comment."
  (fn [chain options]

    (def chain-ref (make-chain-ref chain))
    (load! "rad/monadic/names.rad")
    (def names (ref (chain/load-chain! name-base)))
    (def add-username
      (fn [d]
        (def n (catch
                   'any
                   (lookup :github-username d)
                 (fn [x]
                   (match (lookup-key names (lookup :author d))
                          :nothing "???"
                          'name    name))))
        (insert :username n d)))

    (def add-username-issue
      (fn [i]
        (add-username
         (insert :comments (map add-username (lookup :comments i)) i))))

    (def mk-key
      (fn [v]
        (match v
               (/as 'a {:activity :new-issue
                        :issue    {:number 'n
                                   :title 'tit}})
               [(string-append "[new] Issue " (show n) " - " tit) a]
               (/as 'a {:activity :new-comments
                        :issue    {:number 'n
                                   :title 'tit}})
               [(string-append "[new comments] Issue " (show n) " - " tit) a]
               (/as 'i {:issue {:number 'n :title 'tit}})
               [(string-append "Issue " (show n) " - " tit) i])))

    (def filt
      (match [(lookup-default "open" #f options)
              (lookup-default "closed" #f options)]
             [#t #f] (fn [x] (eq? (lookup :state (lookup :issue x)) :open))
             [#f #t] (fn [x] (eq? (lookup :state (lookup :issue x)) :closed))
             _       (fn [x] #t)))

    (def items
      (map mk-key
           (filter filt
                   (match [(lookup-default "new" #f options) (read-file-key! ".radicle-issues" :last-mark-done)]
                          [#t [:just 't]] (recent-activity chain-ref t)
                          _               (map (fn [i] {:issue i})
                                               (reverse (values (list-issues chain-ref))))))))

    (def items-dict (dict-from-seq items))
    (if (empty-seq? items)
      (put-str! "No recent items!")
      (match (fzf-select-with-preview!
               (map first items)
               (fn [s] (pretty-issue (add-username-issue (lookup :issue (lookup s items-dict))))))
             (/just 's)
             (do (def selected (lookup s items-dict))
                 (def issue-number (match selected {:issue {:number 'n}} n))
                 (def pretty-item
                   (add-comment-md
                    (pretty-issue
                     (add-username-issue
                      (lookup :issue selected)))))
                 (def edited-item (edit-in-editor! pretty-item))
                 (def added-comment
                   (string/unlines
                    (take-while (fn [x] (not (eq? x comment-divider)))
                                (string/lines edited-item))))
                 (put-str!
                  (if (eq? added-comment "")
                    "Item was not updated."
                    (do (simple-add-comment! chain-ref issue-number added-comment)
                        (string-append "Sent comment: " added-comment)))))
             _ (put-str! "No selection made.")))))

(def close-issue
  (fn [issue-number chain-name]
    (def n (read issue-number))
    (if (number? n)
      (if (integral? n)
        (do (def chain-ref (ref (chain/new-chain (make-name chain-name))))
            (simple-edit-issue! chain-ref n {:state :closed}))
        (cmd-parse-failure "Issue numbers must be whole numbers!"))
      (cmd-parse-failure "To close an issue, please provide the issue number."))))

(def new-issue
  (fn [chain]
    (def chain-ref (ref (chain/new-chain (make-name chain))))
    (def template
";; Issue template. An empty or invalid value will abort.
{:title      \"Pick a title\"
 :body       \"Pick a body\"
 :labels     []
}
")
    (def i (read (edit-in-editor! template)))
    (create-issue!
         chain-ref (simple-issue i))))

(def create-chain
  (fn [chain]
    (create-issues-chain! (make-name chain))))

(def make-key
  (fn [name]
    (def kp (gen-key-pair! (default-ecc-curve)))
    (write-file! "my-keys.rad" (show kp))
    (load! "rad/monadic/names.rad")
    (add-name! name-base name (lookup :public-key kp))))

(def names
  (fn []
    (load! "rad/monadic/names.rad")
    (print! (list-names name-base))))

(init-file-dict! ".radicle-issues")

(def cmd-parse-failure
  (fn [error]
    (put-str! error)
    (put-str! help)
    (exit! 1)
  ))

(def /cmd-1
  (fn [cmd cmd-args-pat]
    ((validation/type :string) cmd)
    ((validation/type :atom) cmd-args-pat)
    (fn [value]
      (match value
        [cmd 'cmd-args] (match-pat cmd-args-pat cmd-args)
        [cmd] (cmd-parse-failure (string-append "Missing argument \"" (show cmd-args-pat) "\" for command \"" cmd "\""))
        (/cons cmd _) (cmd-parse-failure (string-append "Too many arguments for command \"" cmd "\""))
        _ :nothing)
    )))

(def /cmd-2
  (fn [cmd arg-1 arg-2]
    ((validation/type :string) cmd)
    ((validation/type :atom) arg-1)
    ((validation/type :atom) arg-2)
    (fn [value]
      (match value
             [cmd 'a-1 'a-2] (match-pat [arg-1 arg-2] [a-1 a-2])
             [cmd _] (cmd-parse-failure (string-append "Missing argument \"" (show arg-2) "\" for command \"" cmd "\""))
             [cmd] (cmd-parse-failure (string-append "Missing argument \"" (show arg-1) "\" for command \"" cmd "\""))
             (/cons cmd _) (cmd-parse-failure (string-append "Too many arguments for command \"" cmd "\""))
             _ :nothing)
      )))

(def /cmd-0
  (fn [cmd]
    ((validation/type :string) cmd)
    (fn [value]
      (match value
        [cmd] [:just {}]
        (/cons cmd _) (cmd-parse-failure (string-append "Too many arguments for command \"" cmd "\""))
        _ :nothing)
    )))

(def valid-list-options
  ["--new" "--open" "--closed"])

(def list-options
  (fn [os]
    (foldr (fn [o acc]
             (if (elem? o valid-list-options)
               (insert (drop 2 o) #t acc)
               (cmd-parse-failure (string-append "Invalid list option: " o))))
           {}
           os)))

(def /list-cmd
  (fn [ch-name opts]
    (fn [v]
      (match v
             ["list"] (cmd-parse-failure "Missing arguments for command \"list\"")
             (/cons "list" (/cons 'c-n 'os)) (if (elem? c-n valid-list-options)
                                               (cmd-parse-failure "Missing \"chain-name\" argument for command \"list\"")
                                               [:just {ch-name c-n
                                                       opts    (list-options os)}])
             _ :nothing))))

(def args (get-args!))
(match args
  (/list-cmd 'chain-name 'options) (list-items chain-name options)
  (/cmd-0 "names") (names)
  (/cmd-1 "make-key" 'nick) (make-key nick)
  (/cmd-1 "new" 'chain-name) (new-issue chain-name)
  (/cmd-1 "create-chain" 'chain-name) (create-chain chain-name)
  (/cmd-2 "close" 'issue-number 'chain-name) (close-issue issue-number chain-name)
  (/cmd-0 "done") (mark-last-seen)
  ["help"] (put-str! help)
  ["-h"] (put-str! help)
  ["--help"] (put-str! help)
  (/cons 'cmd _) (cmd-parse-failure (string-append "Unknown command: " cmd))
  [] (put-str! help))
