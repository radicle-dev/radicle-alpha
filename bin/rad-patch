#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))
(load! (find-module-file! "monadic/patch.rad"))
(load! (find-module-file! "prelude/io-utils.rad"))
(load! (find-module-file! "monadic/project.rad"))

(file-module! "prelude/error-messages.rad")
(import prelude/error-messages :as 'error)

(import prelude/validation :as 'validation)

(def machine-name (fn [] (get-rsm-of-type! :rad-patch)))

(def valid-states
  ["pending" "retracted" "accepted" "rejected"])

(def status-opts ["-s" "--state" "--filter-by-state" ])

(def list-opts-help
  (string-append
    "[("
    (string/intercalate " | " status-opts)
    ") <state>] [--fancy]"))

(def help
  (string-append
  "rad patch - Radicle Patch CLI

   Usage:
        rad patch list " list-opts-help "
        rad patch propose <commit>
        rad patch [show | retract | accept | reject | checkout] <patch-number>
        rad patch comment <patch-number> <comment>
        rad patch help

     list         - Lists all patches
                    The list can be filtered by state:
                      " (string/intercalate ", " valid-states) "
                    The flags for filtering by state:
                      " (string/intercalate ", " status-opts) "
                    Filtering by multiple states is possible via '-s <state1> -s
                    <state2> ...'.
                    If the option '--fancy' is appended, the list is opened via
                    fzf (note that fzf has to be installed).
     propose      - Create a new patch from a single commit
     show         - Show a patch
     comment      - Add a comment to a patch
     retract      - Retract your own patch
                    This command is restricted to the creator of the patch or the
                    maintainer of the project.
     accept       - Accept a patch
                    Automatically merges to master and pushes to origin. This
                    command is restricted to the maintainer of the project.
     checkout     - Checkout a patch locally
                    Creates a new branch 'patch/<patch-number>' and applies the
                    patch to it.
     reject       - Reject a patch
                    This command is restricted to the maintainer of the project.
     help         - Print this help and exit
  "))

(def cmd-parse-failure
  (fn [error]
    (parse-failure error help)))

(def verify-patch-number
  (fn [patch-no machine]
    (match (lookup-default patch-no #f (list-patches machine))
      #f         (do
                   (put-str! (error/item-not-found :patch (show patch-no)))
                   (exit! 1))
      'the-patch  the-patch)))

;; git interactions

(def verify-commit
  (fn [commit]
    (match (process-with-stdout-stderr-exitcode! "git" ["show" commit] "")
      [_ _ :ok] commit
      _         (do
                  (put-str! (error/unknown-commit commit))
                  (exit! 1)))))

(def get-patch!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["format-patch" "-1" commit "--stdout"] ""))))

(def get-title!
  (fn [commit]
    (get-git-commit-data! "%s" commit)))

(def get-description!
  (fn [commit]
    (get-git-commit-data! "%b" commit)))

(def get-author-name!
  (fn [commit]
    (get-git-commit-data! "%aN" commit)))

(def get-short-hash!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["rev-parse" "--short" commit] ""))))

(def get-commit-parent!
  (fn [commit]
    (string/unlines (process-with-stdout! "git" ["rev-parse" (string-append commit "^")] ""))))

(def get-head-origin-master!
  (fn []
    (string/unlines (process-with-stdout! "git" ["rev-parse" "origin/master"] ""))))

(def apply-patch!
  "Given a patch, apply it to the current branch"
  (fn [patch msg]
    (def file (unlines (shell-with-stdout! "mktemp" "")))
    (put-str! file)
    (write-file! file patch)
    (process-git-with-exit! ["am" file] msg)))

(def checkout-patch!
  "Creates a new branch patch/<PATCH-ID> based on the PATCH, and switches to it."
  (fn [machine patch-no]
    (def patch (verify-patch-number patch-no machine))
    (def patch-branch (string-append "patch/" (show patch-no)))
    (process-git-with-exit!
      ["checkout" "-b" patch-branch]
      (error/checkout-new-branch-failure patch-branch))
    (apply-patch!
      (lookup :patch patch)
      (error/applying-patch-failure))))

(def push-patch!
  "Merges the PATCH to master, and pushes the changes."
  (fn [machine patch-no]
    (def patch (verify-patch-number patch-no machine))
    (process-git-with-exit!
      ["checkout" "master"]
      (error/checkout-master-failure))
    (apply-patch!
      (lookup :patch patch)
      (error/applying-accepted-patch-failure))
    (process-git-with-exit!
      ["push"]
      (error/push-patch-failure))))


(def verify-patch
  "Verify that the patch is on top of origin/master"
  (fn [commit]
    (if (eq? (get-commit-parent! commit) (get-head-origin-master!))
      commit
      (do
        (put-str! (error/parent-commit-not-master commit))
        (exit! 1)))))

(def new-patch!
  "Creates a new patch on `machine` with a patch from the given `commit`."
  (fn [machine commit]
    (verify-commit commit)
    (verify-patch commit)
    (def patch (get-patch! commit))
    (def author-name (get-author-name! commit))
    (def author-email (get-author-name! commit))
    (def short-hash (get-short-hash! commit))
    (def title (get-title! commit))
    (def description (get-description! commit))
    (def patch {:short-hash    short-hash
               :title         title
               :description   description
               :author-name   author-name
               :author-email  author-email
               :patch         patch})
    (match (simple-create-patch! machine patch)
           ['n] (put-str! (string-append "Proposing patch #" (show n)
                                         " to " machine))
           _    (put-str! (error/no-number-returned :patch)))))

;; showing patches

(def pretty-state
  (fn [state]
    (drop 1 (show state))))

(def cells
  {:state       10
   :number      10
   :title       50
   :author      20
   :modified-at 20})

(def wrap-cell
  (fn [phrase cell-key]
    (def c-length (lookup cell-key cells))
    (string-append (take c-length (string/pad-right-to c-length phrase)) " ")))

(def pretty-table-header
  (fn []
    (string-append
      (wrap-cell "state" :state)
      (wrap-cell "#" :number)
      (wrap-cell "commit" :title)
      (wrap-cell "author" :author)
      (wrap-cell "updated" :modified-at))))

(def pretty-headline
  (fn [i]
    (match i
      '{:number n
        :title tit
        :state s
        :author-name an
        :modified-at m-a}
      (string/unwords
        [(pretty-state s)
         (show n)
         tit
         an
         m-a]))))

(def pretty-row
  (fn [i]
    (match i
      '{:number n
        :title tit
        :state s
        :author-name an
        :modified-at m-a}
      (string-append
        (wrap-cell (pretty-state s) :state)
        (wrap-cell (show n) :number)
        (wrap-cell tit :title)
        (wrap-cell an :author)
        (wrap-cell m-a :modified-at)))))

(def pretty-comment
 (fn [c]
   (match c
     '{:created-at c-a
       :body b
       :author-name a-n}
     (string-append "### " a-n " [" c-a "]\n\n" b "\n"))))

(def pretty-patch
  (fn [i]
    (match i
      '{:patch p
        :created-at c-a
        :comments cs}
      (string-append
        (pretty-headline i) "\n\n"
        p "\n\n"
        "Comments\n"
        "--------\n\n"
        (string/unlines (map pretty-comment cs))))))

(def comment-divider "----------------------------------------")

(def add-comment-md
  (fn [x]
    (string-append
     "\n" comment-divider "\n"
     "---Add comment above the line.\n\n"
     x)))

(def list-items
  "Lists all patches and filters them by `options`. If `--new` is passed as an
  option only recent items are shown. If `--fancy is passed, the list is opened
  via fzf."
  (fn [machine options]

    (def mk-key
      (fn [patch]
        (if (lookup :fancy options)
          [(pretty-headline patch) patch]
          [(pretty-row patch) patch])))

    (def filt
      (match (map (fn [opt] (read (string-append ":" opt))) (lookup :state options))
        []      (fn [x] #t)
        'args   (fn [x] (elem? (lookup :state x) args))))

  (def items
    (map mk-key
      (filter filt
        (reverse (values (list-patches machine))))))

    (def items-dict (dict-from-seq items))
    (if (empty-seq? items)
      (put-str! "No patches!")
      (if (lookup :fancy options)
        (match (fzf-select-with-preview!
                 (map first items)
                 (fn [s] (pretty-patch (lookup s items-dict))))
               (/just 's)
               (do (def selected (lookup s items-dict))
                   (def patch-number (match selected {:number 'n} n))
                   (def pretty-item
                     (add-comment-md
                       (pretty-patch selected)))
                   (def edited-item (edit-in-editor! pretty-item))
                   (def added-comment
                     (string/unlines
                      (take-while (fn [x] (not (eq? x comment-divider)))
                                  (string/lines edited-item))))
                   (put-str!
                    (if (eq? added-comment "")
                      "Item was not updated."
                      (do (simple-add-comment! machine patch-number added-comment (get-git-username!))
                          (string-append "Sent comment: " added-comment)))))
               _ (put-str! "No selection made."))
         (do
           (put-str! (pretty-table-header))
           (map (fn [d] (put-str! (first d))) items))))))

(def show-patch!
  "Shows a single PATCH `n`"
  (fn [machine n]
    (def patch (verify-patch-number n machine))
    (put-str! (pretty-patch patch))))

(def edit-patch-state!
  "Edit the state of a PATCH. If the new state is `:accepted` the PATCH is then
  also merged to master and the changes pushed."
  (fn [machine n new-state]
    (def edit-state-with-feedback!
      (fn [message]
        (catch 'daemon-error
            (simple-edit-patch! machine n {:state new-state})
          (fn [x]
            (put-str!
             (error/state-change-failure :patch (pretty-state new-state)))
            (exit! 1)))
        (put-str! message)))
    (verify-patch-number n machine)
    (if (eq? new-state :accepted)
      (match (push-patch! machine n)
             :ok (edit-state-with-feedback!
                  (string-append "Patch #" (show n) " has been " (pretty-state new-state) " and merged."))
             _   (exit! 1))
      (edit-state-with-feedback!
       (string-append "Patch #" (show n) " has been " (pretty-state new-state))))))

(def create-comment!
  "Add a new `comment` to PATCH."
  (fn [machine n comment]
    (verify-patch-number n machine)
    (simple-add-comment! machine n comment (get-git-username!))
    (put-str! (string-append "Added comment to patch #" (show n)))))

(def cmd-options
  [
    { :key :state :type :multi-opt :options status-opts :possible-values valid-states :default []}
    { :key :fancy :type :flag :options ["--fancy"] :default #f }
  ])

(def /list-cmd
  (fn [opts]
    (/cmd-opts "list" opts cmd-options help)))

(def args (get-args!))

(def patch-num-pat (/number-string (/integer 'patch-number)))

(machine/catch-daemon!
 (fn []
   (match args
          (/list-cmd 'options) (list-items (machine-name) options)
          (/cmd-1 "propose" 'commit help) (new-patch! (machine-name) commit)
          (/cmd-1 "checkout" patch-num-pat help) (checkout-patch! (machine-name) patch-number)
          (/cmd-1 "checkout" _ help)             (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-1 "show" patch-num-pat help) (show-patch! (machine-name) patch-number)
          (/cmd-1 "show" _ help)                (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-1 "retract" patch-num-pat help) (edit-patch-state! (machine-name) patch-number :retracted)
          (/cmd-1 "retract" patch-num-pat help) (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-1 "accept" patch-num-pat help) (edit-patch-state! (machine-name) patch-number :accepted)
          (/cmd-1 "accept" _ help)                (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-1 "reject" patch-num-pat help) (edit-patch-state! (machine-name) patch-number :rejected)
          (/cmd-1 "reject" _ help)                (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-2 "comment" patch-num-pat 'comment help) (create-comment! (machine-name) patch-number comment)
          (/cmd-2 "comment" _ 'comment help) (cmd-parse-failure (error/whole-item-number :patch))
          (/cmd-0 "create-machine" help) (new-patch-machine! (machine-name))
          ["help"] (put-str! help)
          ["-h"] (put-str! help)
          ["--help"] (put-str! help)
          (/cons 'cmd _) (cmd-parse-failure (error/unknown-command cmd))
          [] (put-str! help))))
