#!/usr/bin/env radicle

(load-ns! "prelude.rad")

(ns rad-key)

(require prelude/patterns '[match-pat _ /cons])
(require prelude/key-management '[create-keys! read-keys!] key-mgmt)
(require prelude/cmd-parsing '[/cmd-0 /cmd-help])

(def help
  "rad key - Radicle Key Management CLI

   Usage:
        rad key create [--force]
        rad key help

   Creates a new key pair. If `--force` is appended, an existing key pair
   file will be overwritten.
   Per default, key pairs are stored in `$HOME/.config/radicle/my-keys.rad`
   this can be adjusted by setting `$XDG_CONFIG_HOME`.

   The key pair is used for signing actions in the apps. Note that some actions
   are restricted to certain key pairs so deleting or overwriting your key pair
   can lead to loss of some authorisations.")

(def make-keys
  (fn []
    (put-str! (string-append "Successfully created " (key-mgmt/create-keys!)))))

(def safe-make-keys
  (fn []
    (match (key-mgmt/read-keys!)
      :nothing  (make-keys)
      _         (put-str! "The `my-keys.rad` file already exists.
If you are sure that you want to replace your key pair, run `rad key create --force`"))))

(def cmd-parse-failure
  (fn [error]
    (parse-failure error help)))

(def args (get-args!))
(match args
  ["create" "--force"]    (make-keys)
  (/cmd-0 "create" help)  (safe-make-keys)
  (/cmd-help)             (put-str! help)
  (/cons 'cmd _)          (cmd-parse-failure (string-append "Unknown command: " cmd))
  _                       (put-str! help))
