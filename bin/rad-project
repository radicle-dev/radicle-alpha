#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))
(load! (find-module-file! "monadic/issues.rad"))
(load! (find-module-file! "monadic/diff.rad"))
(load! (find-module-file! "monadic/project.rad"))
(load! (find-module-file! "prelude/io-utils.rad"))

(import prelude/validation :as 'validation)
(import prelude/cmd-parsing :unqualified)

(def help
  "rad-project - Radicle project CLI

   Usage:
        rad-project init
        rad-project show-id
        rad-project checkout <PROJECT-ID>

     init         - Initialize a new project
     show-id      - Show the project id
     checkout     - Checkout a project
  ")

(def base "http://localhost:8000/machines/")

(def prompt-for-metadata!
  (fn []
    (def name (prompt! "? What's the name of your project: "))
    (def desc (prompt! "? Briefly describe the project: "))
    {:name name :description desc}))

(def cid-of-empty-repo "QmUNLLsPACCz1vLxQVkXqqLX5R1X345qqfHbsf67hvA3Nn")

(def init-git-ipfs-repo
  "Creates a key for a repo. Sets the remote to that. Points the IPNS link to
   the provided CID. Returns the remote."
  (fn [orig-cid]
    (def keyname (uuid!))
    (def key
      (unlines
        (process-with-stdout!
          "rad-ipfs"
          ["key" "gen" "--type=ed25519" keyname]
          "")))
    (process-with-stdout!
      "rad-ipfs"
      ["name" "publish" "--key" key orig-cid]
      "")
    (def remote (string-append "ipfs://ipns/" key))
    remote))

(def checkout!
  "Checkout a project."
  (fn [project]
    (def name (lookup :name (get-meta! project)))
    (def origin (lookup :id (first-rsm-of-type! project :rad-repo)))
    (put-str! name)
    (process! "git" ["clone" origin name] "")
    (cd! name)
    (set-git-config! "radicle.project-id" project)))

(def new-project!
  "Create a new project."
  (fn []
    (def meta (prompt-for-metadata!))
    (def project (create-project! meta))
    (process! "git" ["init"] "")
    (def repo (init-git-ipfs-repo cid-of-empty-repo))
    (put-str! (string-append "=> adding \"origin\" remote: " repo))
    (def issues (create-issues-machine!))
    (put-str! "=> Assembled rad-issues machine")
    (def diff (create-diffs-machine!))
    (put-str! "=> Assembled rad-diff machine")
    (put-str! (string-append "=> project id: " project))
    (process! "git" ["remote" "add" "origin" repo] "")
    (add-rsm! project { :id repo :type :rad-repo })
    (add-rsm! project { :id issues :type :rad-issues })
    (add-rsm! project { :id diff :type :rad-diff })
    (set-git-config! "radicle.project-id" project)
    (put-str! "=> project created!")))


(def show-project-id
  (fn []
    (put-str! (string-append "=> " (get-project-url!)))
    (put-str! "=> Share this project id for people to collaborate with you on your project.")))

(def args (get-args!))
(match args
  (/cmd-0 "init" help) (new-project!)
  (/cmd-0 "show-id" help) (show-project-id)
  (/cmd-1 "checkout" 'project-id help) (checkout! project-id)
  ["help"] (put-str! help)
  ["-h"] (put-str! help)
  ["--help"] (put-str! help)
  (/cons 'cmd _) (parse-failure (string-append "Unknown command: " cmd) help)
  [] (put-str! help))
