{:module 'prelude/chain
 :doc "Functions for simulating remote chains."
 :exports '[new-chain load-chain! eval-in-chain update-chain-ref! eval updatable-eval update-chain! eval-fn-app send-prelude!]}

(import prelude/io '[read-file-values!] :as 'io)
(import prelude/ref '[modify-ref] :unqualified)
(import prelude/lens '[.. ... view over @] :unqualified)
(import prelude/basic :unqualified)

;; Chains: Functions for dealing with chains.

;; A chain is conceptually:
;;   - A known starting state (i.e. environment, and the assumption that `eval
;;     == base-eval')
;;   - A sequence of inputs.

(def @var
  "A lens for variables in states of chains."
  (fn [ident]
    (... [(@ :env) (@ ident) (@ :val)])))

(def new-chain
  "Return an empty chain dictionary with the given url."
  (fn [url]
    {:state (pure-env)
     :inputs []
     :url url
     :index :nothing}))

;; eval-in-chain

(def eval-in-chain
  "Evaluates `expr` in the `chain` and returns a dict with the `:result` and the resulting `:chain`."
  (fn [expr chain]
    (def chain-eval (view (.. (@ :state) (@var 'eval)) chain))
    (def x (chain-eval expr (lookup :state chain)))
    (def result (nth 0 x))
    (def new-state (nth 1 x))
    (def new-chain
      (insert
        :state
        new-state
        (over (@ :inputs)
              (fn [inputs] (add-right expr inputs))
               chain)))
    {:chain new-chain
     :result result }))

(def local-test-chain-name
  (fn [] (string-append "http://localhost:8000/chains/" (uuid!))))

(:test "eval-in-chain"
  [:setup
    (do (def empty-chain (new-chain (local-test-chain-name)))
        (def res1 (eval-in-chain '(def x 3) empty-chain))
        (def res (eval-in-chain '(+ x 2) (lookup :chain res1))))
  ]
  [ (view (@ :result) res)                 ==> 5                   ]
  [ (view (.. (@ :chain) (@ :inputs)) res) ==> [(def x 3) (+ x 2)] ]
)

;; update-chain!

(def update-chain!
  "Takes a chain, and returns a new chain updated with the new expressions from the remote chain"
  (fn [chain]
    (def index (length (lookup :inputs chain)))
    (def receive-result
      (receive! (lookup :url chain) (lookup :index chain)))
    (def new-index (nth 0 receive-result))
    (def new-inputs (nth 1 receive-result))
    (def upd-ch
      (fn [ch expr]
        (def x (eval-in-chain expr ch))
        (view (@ :chain) x)))
    (def chain (foldl upd-ch chain new-inputs))
    (insert :index [:just new-index] chain)))

(:test "update-chain!"
  [:setup
    (do
       (def chain-name (local-test-chain-name))
       (def chain (new-chain chain-name))
       (send! chain-name ['(+ 3 2)])
       (send! chain-name ['(+ 3 3)])
       (def chain (update-chain! chain)))
  ]
  [ (lookup :inputs chain) ==> [(+ 3 2) (+ 3 3)]]
)

(def load-chain!
  "Takes a `url`, and fetches the inputs of a remote chain and return a chain dictionary with the chain state."
  (fn [url]
    (update-chain! (new-chain url))))

;; update-chain-ref!

(def update-chain-ref!
  "Update `chain-ref` containing a chain with the new expressions from the remote chain"
  (fn [chain-ref]
    (modify-ref chain-ref update-chain!)
    :ok))

(:test "update-chain-ref!"
  [:setup
    (do
       (def chain-name (local-test-chain-name))
       (def chain (ref (new-chain chain-name)))
       (send! chain-name ['(+ 3 2)])
       (send! chain-name ['(+ 3 3)])
       (update-chain-ref! chain))
  ]
  [ (lookup :inputs (read-ref chain)) ==> [(+ 3 2) (+ 3 3)]]
)

;; add-quit

(def add-quit
  "Adds a `:quit` command to `before-quit-eval`, which switches to `after-quit-state` (and to the eval in that state)"
  (fn [after-quit-state before-quit-eval]
    (fn [expr env]
      (if (eq? expr :quit)
          (list :ok after-quit-state)
          (before-quit-eval expr env)))))

;; store-exprs

(def store-exprs
  "Store each new evaluated expression in `_inputs`"
 (fn [evalfn]
     (fn [expr env]
         (def env_ (over (@var '_inputs) (fn [x] (add-right expr x)) env))
         (evalfn expr env_ ))))

;; enter-remote-chain!
(def enter-remote-chain!
  "Make the eval behave as that of a remote chain. The second param is the env to return to after `:quit`."
  (fn [url env]
    (def chain (load-chain! url))
    (def chain-state (lookup :state chain))
    (def mod-state1
      (over (@ :env) (fn [x] (insert '_inputs {:val []} x)) chain-state))
    (def mod-state2
      (over (@ :env)
            (fn [x] (insert '_cur-chain {:val (lookup :url chain)} x))
            mod-state1))
    (def mod-state3
      (over (@var 'eval)
            (fn [x] (add-quit env x))
            mod-state2))
    (list :ok mod-state3)))

(def eval__ "The eval in place when `chain.rad` is loaded." eval)

(def eval
  "An eval in which one can use `(:enter-chain url)` to make the
  eval behave as that of a remote chain, and `:send` to send all
  enqueued expressions."
  (fn [expr env]
      (if (list? expr)
          (cond
            (eq? (first expr) :enter-chain)
              (enter-remote-chain! (first (eval__ (nth 1 expr) env)) env)
            :else (eval__ expr env))
          (eval__ expr env))))

(def eval-fn-app
  "Given a state, a function, an argument and a callback, returns
  the result of evaluating the function call on the arg in the given
  state, while also calling the callback on the result."
  (fn [state f arg cb]
    (def exec (base-eval (list f (list 'quote arg)) state))
    (cb (first exec))
    exec))

(def updatable-eval
  "Given an evaluation function `f`, returns a new one which augments `f` with a new
  command `(update expr)` which evaluates arbitrary expression using `base-eval`."
  (fn [sub-eval]
    (fn [expr state]
      (match expr
             (/cons 'cmd _) (if (eq? cmd 'update)
                              (base-eval (nth 1 expr) state)
                              (sub-eval expr state))
             _ (sub-eval expr state)))))

(def pure-prelude-files
  "List of files which together define the pure prelude."
  ["rad/prelude/basic.rad"
   "rad/prelude/patterns.rad"
   "rad/prelude/strings.rad"
   "rad/prelude/bool.rad"
   "rad/prelude/list.rad"
   "rad/prelude/dict.rad"
   "rad/prelude/set.rad"
   "rad/prelude/ref.rad"
   "rad/prelude/lens.rad"
   "rad/prelude/state-machine.rad"
   "rad/prelude/validation.rad"
   "rad/prelude/util.rad"])

(def file-to-module
  "Make the text of a file into code for a module."
  (fn [file]
    (cons 'module (vec-to-list (io/read-file-values! file)))))

(def pure-prelude-code
  "Code for the pure prelude. Useful as the first inputs to a new chain."
  (<> [(file-to-module "rad/prelude/test-eval.rad")
       '(import prelude/test-eval '[eval tests] :unqualified)]
      (map file-to-module pure-prelude-files)))

(def send-prelude!
  "Send the pure prelude to a chain."
  (fn [chain-id]
    (send! chain-id pure-prelude-code)))
